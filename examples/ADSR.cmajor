graph ADSRExample [[ main ]]
{
    input event std::midi::Message midi;
    input ADSR.attack;
    input ADSR.decay;
    input ADSR.sustain;
    input ADSR.release;
    output stream float out;

    connection {
        midi -> std::midi::MPEConverter -> ADSR.noteIn;
        ADSR -> out;
    }
}

graph ADSR 
{
    input event (std::notes::NoteOn, std::notes::NoteOff) noteIn;
    input event float attack [[name: "Attack", min: 0.0, max: 10.0, init:0.3]];
    input event float decay [[name: "Decay", min: 0.0, max: 10.0, init: 0.2]];
    input event float sustain [[name: "Sustain", min: 0.0, max: 1.0, init: 0.70]];
    input event float release [[name: "Release", min: 0.0, max: 10.0, init: 0.3]];
    output stream float out;

    node { 
        att = EnvelopeBuilder::Segments::EnvelopeSegment(1.f, 0.3f);
        dec = EnvelopeBuilder::Segments::EnvelopeSegment(1.f, 0.7f);
        sus = EnvelopeBuilder::Segments::SustainSegment;
        rel = EnvelopeBuilder::Segments::EnvelopeSegment(0.f, 0.7f);
    }

    event noteIn(std::notes::NoteOn on) {
        att.latch <- void;
        dec.free <- void;
        sus.free <- void;
        rel.free <- void;
    }

    event noteIn(std::notes::NoteOff off) {
        att.free <- void;
        rel.latch <- void;
    }

    connection {
        attack -> att.seconds;
        decay -> dec.seconds;
        sustain -> dec.targetLevelIn;
        release -> rel.seconds;
        
        att.trigger -> dec.latch;
        dec.trigger -> sus.latch;

        
        att.out -> dec.in;
        dec.out -> sus.in;
        sus.out -> rel.in;
        rel.out -> out;
    }
}