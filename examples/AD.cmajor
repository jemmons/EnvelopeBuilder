graph ADExample [[ main ]]
{
    input event std::midi::Message midi;
    input AD.attack;
    input AD.decay;
    output stream float out;

    connection {
        midi -> std::midi::MPEConverter -> AD.noteIn;
        AD -> out;
    }
}

graph AD 
{
    input event (std::notes::NoteOn, std::notes::NoteOff) noteIn;
    input value float attack [[name: "Attack", min: 0.0, max: 10.0, init:0.3]];
    input value float decay [[name: "Decay", min: 0.0, max: 10.0, init: 0.2]];
    output stream float out;

    node { 
        att = EnvelopeBuilder::Segments::EnvelopeSegment;
        dec = EnvelopeBuilder::Segments::EnvelopeSegment;
    }

    event noteIn(std::notes::NoteOn on) {
        att.latch <- void;
        dec.free <- void;
    }

    event noteIn(std::notes::NoteOff off) {
        att.free <- void;
        dec.latch <- void;
    }

    connection {
        1.f -> att.targetLevel;
        0.f -> dec.targetLevel;

        0.3f -> att.curve;
        0.7f -> dec.curve;

        attack -> att.seconds;
        decay -> dec.seconds;
        
        att.trigger -> dec.latch;

        att.out -> dec.in;
        dec.out -> out;
    }
}