namespace EnvelopeBuilder {
  namespace Envelopes {
    graph ADSR {
      input event (std::notes::NoteOn, std::notes::NoteOff) noteIn;
      input value float attack [[name: "Attack", min: 0.0, max: 10.0, init:0.3]];
      input value float decay [[name: "Decay", min: 0.0, max: 10.0, init: 0.2]];
      input value float sustain [[name: "Sustain", min: 0.0, max: 1.0, init: 0.70]];
      input value float release [[name: "Release", min: 0.0, max: 10.0, init: 0.3]];
      output stream float out;

      node { 
        att = Segments::EnvelopeSegment;
        dec = Segments::EnvelopeSegment;
        sus = Segments::SustainSegment;
        rel = Segments::EnvelopeSegment;
        clipper = Components::ClipNormal;
      }

      event noteIn(std::notes::NoteOn on) {
        att.capture <- void;
      }

      event noteIn(std::notes::NoteOff off) {
        att.halt <- void;
        dec.halt <- void;
        sus.halt <- void;
      }

      connection {
        1.f -> att.targetLevel;
        0.f -> rel.targetLevel;

        attack -> att.seconds;
        decay -> dec.seconds;
        sustain -> dec.targetLevel;
        release -> rel.seconds;
        
        att.trigger -> dec.capture;
        dec.trigger -> sus.capture;
        sus.trigger -> rel.capture;
        
        att.out -> dec.in;
        dec.out -> sus.in;
        sus.out -> rel.in;
        rel.out -> clipper -> out;
      }
    }
  }
}