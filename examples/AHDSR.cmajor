graph AHDSRExample [[ main ]]
{
    input event std::midi::Message midi;
    input AHDSR.attack;
    input AHDSR.hold;
    input AHDSR.decay;
    input AHDSR.sustain;
    input AHDSR.release;
    output stream float out;

    connection {
        midi -> std::midi::MPEConverter -> AHDSR.noteIn;
        AHDSR -> out;
    }
}

graph AHDSR 
{
    input event (std::notes::NoteOn, std::notes::NoteOff) noteIn;
    input event float attack [[name: "Attack", min: 0.0, max: 10.0, init:0.3]];
    input event float hold [[name: "Hold", min: 0.0, max: 10.0, init:0.0]];
    input event float decay [[name: "Decay", min: 0.0, max: 10.0, init: 0.2]];
    input event float sustain [[name: "Sustain", min: 0.0, max: 1.0, init: 0.70]];
    input event float release [[name: "Release", min: 0.0, max: 10.0, init: 0.3]];
    output stream float out;

    node { 
        att = EnvelopeBuilder::Segments::EnvelopeSegment(1.f, 0.3f);
        hol = EnvelopeBuilder::Segments::EnvelopeSegment(1.f);
        dec = EnvelopeBuilder::Segments::EnvelopeSegment(1.f, 0.7f);
        sus = EnvelopeBuilder::Segments::SustainSegment;
        rel = EnvelopeBuilder::Segments::EnvelopeSegment(0.f, 0.7f);
    }

    event noteIn(std::notes::NoteOn on) {
        att.latch <- void;
        hol.free <- void;
        dec.free <- void;
        sus.free <- void;
        rel.free <- void;
    }

    event noteIn(std::notes::NoteOff off) {
        att.free <- void;
        rel.latch <- void;
    }

    connection {
        attack -> att.seconds;
        hold -> hol.seconds;
        decay -> dec.seconds;
        sustain -> dec.targetLevelIn;
        release -> rel.seconds;
        
        att.trigger -> hol.latch;
        hol.trigger -> dec.latch;
        dec.trigger -> sus.latch;

        att.out -> hol.in;
        hol.out -> dec.in;
        dec.out -> sus.in;
        sus.out -> rel.in;
        rel.out -> out;
    }
}